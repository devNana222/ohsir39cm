plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.tdd'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}
ext{
    set('snippetsDir', file("build/generated-snippets"))
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2' //테스트시에 사용하는 내장Database
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    //implementation 'com.google.guava:guava:11.0.2'
    //swagger
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0")
    //restdocs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    //redis
    implementation 'org.redisson:redisson-spring-boot-starter:3.16.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    //docker
    testImplementation "org.testcontainers:testcontainers:1.20.3"
    testImplementation "org.testcontainers:junit-jupiter:1.20.3"
    testImplementation "org.testcontainers:mysql:1.20.3"
}

// 전역 변수 설정 시작
ext {
    snippetsDir = file('build/generated-snippets')
    queryDslVersion = "5.0.0"
}
// 전역 변수 설정 끝

// rest docs 추가 시작
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {

    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources {
        exclude ('**/api/**')
    }

    baseDirFollowsSourceDir() // 다른 adoc 파일을 include 할 때 경로를 baseDir 로 맞춘다.
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/rest-docs'
    }
}
// rest docs 추가 끝

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}
